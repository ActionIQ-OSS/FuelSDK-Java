//
// This file is part of the Fuel Java SDK.
//
// Copyright (C) 2013, 2014 ExactTarget, Inc.
// All rights reserved.
//
// Permission is hereby granted, free of charge, to any person
// obtaining a copy of this software and associated documentation
// files (the "Software"), to deal in the Software without restriction,
// including without limitation the rights to use, copy, modify,
// merge, publish, distribute, sublicense, and/or sell copies
// of the Software, and to permit persons to whom the Software
// is furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be
// included in all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY
// KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE
// WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR
// PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS
// OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES
// OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT
// OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH
// THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//

options
{
    static = false;
}

PARSER_BEGIN(ETFilterParser)
package com.exacttarget.fuelsdk;

public class ETFilterParser {
}
PARSER_END(ETFilterParser)

SKIP: { " " | "\n" | "\r" | "\t" }

TOKEN [IGNORE_CASE]:
{
    < AND : "and" >
    | < OR : "or" >
    | < NOT : "not" >
    | < BETWEEN : "between" >
    | < IN : "in" >
    | < IS : "is" >
    | < LIKE : "like" >
    | < NULL : "null" >
}

TOKEN:
{
    < UNQUOTED_STRING : (~[" ", "\n", "\r", "\t", "'", "\"", "=", "!", "<", ">", "(", ")", ","])+ >
    | < SINGLE_QUOTED_STRING : "'" (~["\n", "\r", "'"])* "'" >
    | < DOUBLE_QUOTED_STRING : "\"" (~["\n", "\r", "\""])* "\"" >
}

ETFilter parse(Class<? extends ETObject> type) throws ETSdkException:
{
    ETFilter filter = null;
}
{
    (
        LOOKAHEAD(BetweenExpression(type)) filter = BetweenExpression(type)
        | LOOKAHEAD(InExpression(type)) filter = InExpression(type)
        | LOOKAHEAD(IsNullExpression(type)) filter = IsNullExpression(type)
        | filter = ComparisonExpression(type)
    )
    {
        return filter;
    }
}

ETFilter BetweenExpression(Class<? extends ETObject> type) throws ETSdkException:
{
    ETFilter filter = new ETFilter();
    String value = null;
}
{
    value = Value()
    {
        if (type != null) {
            filter.setProperty(ETObject.getInternalProperty(type, value));
        } else {
            filter.setProperty(value);
        }
    }

    < BETWEEN >
    {
        filter.setOperator("between");
    }

    value = Value()
    {
        filter.addValue(value);
    }

    < AND >

    value = Value()
    {
        filter.addValue(value);
    }

    {
        return filter;
    }
}

ETFilter ComparisonExpression(Class<? extends ETObject> type) throws ETSdkException:
{
    ETFilter filter = new ETFilter();
    String value = null;
}
{
    value = Value()
    {
        if (type != null) {
            filter.setProperty(ETObject.getInternalProperty(type, value));
        } else {
            filter.setProperty(value);
        }
    }

    (
        "="
        {
            filter.setOperator("=");
        }
        | "!="
        {
            filter.setOperator("!=");
        }
        | "<"
        {
            filter.setOperator("<");
        }
        | "<="
        {
            filter.setOperator("<=");
        }
        | ">"
        {
            filter.setOperator(">");
        }
        | ">="
        {
            filter.setOperator(">=");
        }
        | < LIKE >
        {
            filter.setOperator("like");
        }
    )

    value = Value()
    {
        filter.addValue(value);
    }

    {
        return filter;
    }
}

ETFilter InExpression(Class<? extends ETObject> type) throws ETSdkException:
{
    ETFilter filter = new ETFilter();
    String value = null;
}
{
    value = Value()
    {
        if (type != null) {
            filter.setProperty(ETObject.getInternalProperty(type, value));
        } else {
            filter.setProperty(value);
        }
    }

    < IN >
    {
        filter.setOperator("in");
    }

    "("

    value = Value()
    {
        filter.addValue(value);
    }

    (
        ","

        value = Value()
        {
            filter.addValue(value);
        }
    )+

    ")"

    {
        return filter;
    }
}

ETFilter IsNullExpression(Class<? extends ETObject> type) throws ETSdkException:
{
    ETFilter filter = new ETFilter();
    String value = null;
}
{
    value = Value()
    {
        if (type != null) {
            filter.setProperty(ETObject.getInternalProperty(type, value));
        } else {
            filter.setProperty(value);
        }
    }

    < IS >

    (
        < NULL >
        {
            filter.setOperator("is null");
        }
        | < NOT > < NULL >
        {
            filter.setOperator("is not null");
        }
    )

    {
        return filter;
    }
}

String Value():
{
    Token token = null;
}
{
    (
        token = < UNQUOTED_STRING >
        {
            return token.image;
        }
        | token = < SINGLE_QUOTED_STRING >
        {
            return token.image.substring(1, token.image.length() - 1);
        }
        | token = < DOUBLE_QUOTED_STRING >
        {
            return token.image.substring(1, token.image.length() - 1);
        }
    )
}
